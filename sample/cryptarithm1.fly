(*----------------------------------------*
 * Cryptarithm: DOG + ANT = CAT
 *----------------------------------------*)

!map = @ { !self = $$; !f = $$; !lis = $$;
  {CASE:
    { ![] = lis; $CASE: -> [] };
    { !x::xs = lis; f x :: self f xs }
  }
};

!remove = @ { !self = $$; !x = $$; !lis = $$;
  {CASE:
    { ![] = lis; $CASE: -> [] };
    { !y::ys = lis; ?x=y; $CASE: -> self x ys };
    { !y::ys = lis; y :: self x ys }
  }
};

!each = { !lis = $$;
  $P: k -> map { !x = $$; k [x, remove x lis] } lis
};

!main = { $$;
  ![x, y, z] = {BR:
    {P:
      ![d, rest] = each [0, 1, 2, 3, 4, 5, 6, 7, 8, 9];
      ![o, rest] = each rest;
      ![g, rest] = each rest;
      ![a, rest] = each rest;
      ![n, rest] = each rest;
      ![t, rest] = each rest;
      ![c, _]    = each rest;
      !x = d * 100 + o * 10 + g;
      !y = a * 100 + n * 10 + t;
      !z = c * 100 + a * 10 + t;
      { ? x > 100;
        ? y > 100;
        ? z > 100;
        ? x + y = z;
        $BR: -> [x, y, z] }
    }
  };
  writeln "DOG + ANT = CAT";
  print x;
  write " + ";
  print y;
  write " = ";
  println z;
};

!_ = main [];

